idf30 = _{ SOI ~ header_section ~ section+ ~ EOI }

integer = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
float   = @{ integer ~ "." ~ ASCII_DIGIT+ }

string     = ${ ASCII_ALPHA ~ char* }
string_num_allowed = ${ ASCII_ALPHANUMERIC ~ char* }
char             =  _{ ASCII_ALPHANUMERIC | "_" | "." | "/" | ":" | "?" | "-" }
quoted_string   = ${ ("'" | "\"") ~ (!("'" | "\"") ~ ANY)* ~ ("'" | "\"") }

section_name   = { "." ~ string }
section_header =  { section_name ~ string* ~ NEWLINE }
header_section = { section_header ~ string_only_record+ ~ section_name ~ NEWLINE }
section        =  { section_header ~ record* ~ section_name ~ (NEWLINE | EOI) }

value  = _{ string | float | integer | quoted_string }
record =  { value+ ~ NEWLINE }
string_only_record = { (string_num_allowed | quoted_string)+ ~ NEWLINE }

WHITESPACE = _{ " " | "\t" }
COMMENT    = _{ "#" ~ ANY* ~ NEWLINE }
